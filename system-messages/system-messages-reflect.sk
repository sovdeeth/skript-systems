on load:
    set {sys-colour::main} to "&7"
    set {sys-colour::accent} to "&8"
    set {sys-colour::info} to "&7"
    set {sys-colour::warning} to "&c"
    set {sys-colour::error} to "&4"
    set {sys-colour::success} to "&2"

options:
    cm: %{sys-colour::main}%
    ca: %{sys-colour::accent}%
    prefix: [!]

expression:
    patterns:
        (system|sys)( |-)(message|msg) [with text] %text% [(with|of|and)] type %text% [[(with|and)] (objects|inputs) %-objects%]
    parse:
        set {_type} to "%expr-2%"
        replace all """" with "" in {_type}
        if ("info","error","warning", or "success") contains {_type}:
            continue
        stop
    get:
        set {_content} to "%expr-1%"
        set {_vars::*} to expr-3 if expr-3 is set

        set {_prefix} to formatted "%{sys-colour::%{_type}%}%{@prefix}{@cm}"
    
        if {_vars::1} is not set:
            return formatted "%{_prefix}% %{_content}%"
        
        loop {_vars::*}:
            replace "(%loop-index%)" with "%loop-value%{@cm}" in {_content}
            replace "[%loop-index%]" with "{@ca}%loop-value%{@cm}" in {_content}
            replace "{%loop-index%}" with """{@ca}%loop-value%{@cm}""" in {_content}
        
        return formatted "%{_prefix}% %{_content}%" 


command /systemMessageExample:
    permission: server.admin
    trigger:
        send sys-msg "[1]'s nickname is {2}" of type "info" with inputs (player, "Sovde") to player