options:
    ca: %{colour::accent}%
    default-name-colour: &f

    # Message formats
    # the message is composed of prefix followed by separator followed by message
    # The default prefix is a coloured ‚Ä¢ followed by the player's name
    # The default separator is a -
    # A default message looks like " ‚Ä¢ Username - Message text"
    
    # By default the prefix and name coloured are computed per-player in the chat skript, so they are variables here. Be wary of changing these.
    # Separator and text colour as always the same, so change those as much as you like.

    prefix-colour: %{_prefix-colour}% 
    prefix: %{_prefix-tooltip}% ‚Ä¢ %{_name-colour}%%{_player-name}% 

    separator-colour: &7
    separator: -

    text-colour: <##cccccf>
    text: %{_message-tooltip}%%{_message}%

    /me-format: &7&o

    # prefix colours - change these to change the ‚Ä¢'s colour

    staff-colour: &6
    default-colour: &7

    # - tooltips -  
    # Player Info Card -- Appearance
    player line 1: %{_name-colour}%%{_sender}%
    player line 2: &7Nickname: &f%{chat::%{_sender}%::nickname} if {chat::%{_sender}%::nickname} is set else {_sender}%
    player line 3: &7Rank: &f%{chat::%{_sender}%::rank} if {chat::%{_sender}%::rank} is set else "Default"%
    player line 4: &8Click to message.
    
    # Message Info Card -- Appearance
    message line 1: &7Message from %{_name-colour}%%{_sender}%
    message line 2: &7Message Type: &f%{_type}%
    message line 3: &7Time: &f%now%
    message line 4: &8Click to copy text to your chat box.

    # Mention sound

    mention-sound: "block.note_block.bell" with pitch 1.4

    # accented characters

    accChar: √†√®√¨√≤√π√Ä√à√å√í√ô√°√©√≠√≥√∫√Ω√Å√â√ç√ì√ö√ù√¢√™√Æ√¥√ª√Ç√ä√é√î√õ√£√±√µ√É√ë√ï√§√´√Ø√∂√º√ø√Ñ√ã√è√ñ√ú≈∏√ß√á√ü√ò√∏√Ö√•√Ü√¶≈ì

#--------------------------------------------------------------------------

# ----- Commands -----
command /nick <text> [<player=%player%>]:
    permission: core.nickname
    trigger:
        if arg-2 isn't player:
            if player doesn't have permission "core.admin":
                send sm("error","You do not have permission to change others' nicknames.")
                stop

        if arg-1 doesn't match "[A-Z a-z{@accChar}]+": 
            send sm("error","Nicknames may not include symbols or numbers.")
        else if length of arg-1 is greater than 16:
            send sm("error","Nicknames must be 16 characters at most.")
        else:
            send sm("success","Set your nickname to {1}.",(arg-1))
            set {chat::%arg-2%::nickname} to arg-1

command /set-name-colour <player> <text>:
    permission: core.admin
    trigger:
        if arg-2 doesn't match "[A-Fa-f0-9]+":
            send sm("error","Colours must be hexadecimal values.")
        else if (length of arg 2) is not 6:
            send sm("error","Colours must be 6 hexadecimal digits long.")
        else:
            send sm("success","Set [1]'s name colour to (2).",(arg-1, formatted "<##%arg-2%>##%arg-2%"))
            set {chat::%arg-1%::name-colour} to formatted "<##%arg-2%>"
        
# ----- capture chat messages -----

on chat:
    cancel event 
    
    # redirect messages for commands/other functions
    # example code to trigger this given in the README
    if {chat::redirect::%player%} is set:
        set {chat::redirect::%player%::message} to message
        clear {chat::redirect::%player%}
        stop
    
    chat(player, message)

command /me <text>:
    trigger:
        chat(player, arg-1, "/me")



# ----- Format and Send Chat Messages -----

function chat(sender: player, message: text, type: string = "standard"):
    
    # check for muted

    if {chat::muted::%{_sender}%} is set:
        send sm("error","You're muted and can't chat.") to {_sender}
        stop
    
    # set up name

    set {_name-colour} to {chat::%{_sender}%::name-colour}
    set {_player-name} to {chat::%{_sender}%::nickname} if {chat::%{_sender}%::nickname} is set else {_sender}

    # staff colours

    set {_prefix-colour} to "{@staff-colour}" if {staff::*} contains {_sender} else "{@default-colour}"

    # Formatting

    replace all "<3" with "‚ù§" in {_message}
    replace all ">" with "‚Üí" in {_message}
    replace all "<" with "‚Üê" in {_message}

    replace all ":sword:" with "üó°" in {_message}
    replace all ":bow:" with "üèπ" in {_message}
    replace all ":trident:" with "üî±" in {_message}
    replace all ":potion:" with "üß™" in {_message}
    replace all ":splash_potion:" with "‚öó" in {_message}
    replace all ":fishing_rod:" with "üé£" in {_message}
    replace all ":shield:" with "üõ°" in {_message}
    replace all ":axe:" with "ü™ì" in {_message}

    replace all "$i" with formatted "&o" in {_message}
    replace all "$b" with formatted "&l" in {_message}
    replace all "$r" with formatted "&r{@text-colour}" in {_message}

    # set up tooltips

    set {_prefix-tooltip} to formatted "<suggest command:/msg %{_sender}% ><tooltip:{@player line 1}%nl%{@player line 2}%nl%{@player line 3}%nl%{@player line 4}>"
    set {_message-tooltip} to formatted "<suggest command:%{_message}%><tooltip:{@message line 1}%nl%{@message line 2}%nl%{@message line 3}%nl%{@message line 4}>"

    # send messages

    if {_type} is "/me":
        set {_name-colour} to "{@/me-format}"
        send formatted "{@prefix-colour}{@prefix} {@text}" to all players
        send "* %{_sender}% %{_message}%" to console
    else:
        send formatted "{@prefix-colour}{@prefix} {@separator-colour}{@separator} {@text-colour}{@text}" to all players
        send "<%{_sender}%>  %{_message}%" to console
    
    # mentions

    play sound {@mention-sound} to all players where [{_message} contains "%input%"]


# ----- Leave/Join Messages and Housekeeping -----

on join:
    set {chat::%player%::name-colour} to "{@default-name-colour}" if {chat::%player%::name-colour} is not set
    if player has permission "server.staff":
        add player to {staff::*} if {staff::*} doesn't contain player

    set join message to ""
    send formatted " &a‚Üí%{chat::%player%::name-colour}%&o %player%&7&o has {@ca}&ojoined &7&othe server." to all players

on leave:
    set quit message to ""
    send formatted " &c‚Üê%{chat::%player%::name-colour}%&o %player%&7&o has {@ca}&oleft &7&othe server." to all players

